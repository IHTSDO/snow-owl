grammar com.b2international.snowowl.snomed.ecl.Ecl

generate ecl "http://www.b2international.com/snowowl/snomed/Ecl"

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

ExpressionConstraint hidden(WS, SL_COMMENT, ML_COMMENT):
	SimpleExpressionConstraint;
	
SimpleExpressionConstraint:
	DescendantOf | DescendantOrSelfOf | FocusConcept;
	
// XXX FocusConcept Grouping rule no actual EObject present with this name
FocusConcept returns SimpleExpressionConstraint:
	MemberOf | ConceptReference | Any;

DescendantOf:
	LESS_THAN constraint=(FocusConcept | NestableExpression);

DescendantOrSelfOf:
	DBL_LESS_THAN constraint=(FocusConcept | NestableExpression);

MemberOf:
	CARET constraint=(ConceptReference | Any);

ConceptReference: 
	id=SnomedIdentifier (PIPE term=Term PIPE)?;
	
Any: 
	WILDCARD {Any};
	
NestableExpression:
	ROUND_OPEN ExpressionConstraint ROUND_CLOSE;

// hidden grammar rules
SnomedIdentifier 	hidden() : DIGIT_NONZERO (DIGIT_NONZERO|ZERO)(DIGIT_NONZERO|ZERO)(DIGIT_NONZERO|ZERO)(DIGIT_NONZERO|ZERO)(DIGIT_NONZERO|ZERO)+;
Term 				hidden() : (TermCharacter)+ (WS+ (TermCharacter)+)*;
TermCharacter 		hidden() : LESS_THAN|GREATER_THAN
								|DBL_LESS_THAN|DBL_GREATER_THAN
								|AND|OR|NOT
								|ZERO|DIGIT_NONZERO
								|LETTER|CARET
								|EQUAL|PLUS
								|CURLY_OPEN|CURLY_CLOSE
								|ROUND_OPEN|ROUND_CLOSE
								|SQUARE_OPEN|SQUARE_CLOSE
								|DOT|COLON|COMMA
								|OTHER_CHARACTER;

// ---TERMINALS---

// bool operators
terminal AND 					: 'AND';
terminal OR 					: 'OR';

// numeric terminals
terminal ZERO 					: '0';
terminal DIGIT_NONZERO  		: '1'..'9';

// character terminals
terminal LETTER					: ('a'..'z' | 'A'..'Z');
terminal PIPE 					: '|';
terminal COLON 					: ':';
terminal CURLY_OPEN 			: '{';
terminal CURLY_CLOSE			: '}';
terminal EQUAL					: '=';
terminal COMMA					: ',';
terminal ROUND_OPEN 			: '(';
terminal ROUND_CLOSE 			: ')';
terminal SQUARE_OPEN 			: '[';
terminal SQUARE_CLOSE 			: ']';
terminal PLUS 					: '+';
terminal CARET 					: '^';
terminal NOT 					: '!';
terminal DOT					: '.';
terminal WILDCARD 				: '*';
terminal LESS_THAN				: '<';
terminal GREATER_THAN			: '>';
terminal DBL_LESS_THAN			: '<<';
terminal DBL_GREATER_THAN		: '>>';

// comment terminals
terminal WS 					: (' '|'\t'|'\n'|'\r');
terminal ML_COMMENT				: '/*' -> '*/';
terminal SL_COMMENT 			: '//' !('\n'|'\r')* ('\r'? '\n')?;

// misc
terminal OTHER_CHARACTER:		.;
